#!/bin/env perl

# Copyright (c) 2017 Todd Freed <todd.freed@gmail.com>
# 
# This file is part of autominer.
# 
# autominer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# autominer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License

#
# autominer wrapper for ethminer from https://github.com/Genoil/cpp-ethereum
#

use strict;
use warnings;

use POSIX;

if($ARGV[0] eq "configured")
{
  system("ethminer -V 1>/dev/null 2>&1");
  print "yes\n" if $? == 0;
}
elsif($ARGV[0] eq "algos")
{
  print "daggerhashimoto\n";
}
elsif($ARGV[0] eq "mine")
{
  my @cmd = (
      "ethminer"
    , "-SP" , "2"
    , "-U"
    , "-S", "$ENV{AUTOMINER_ALGO}.usa.nicehash.com:$ENV{AUTOMINER_PORT}"
    , "-O", "$ENV{AUTOMINER_USERNAME}"
  );

  if(defined $ENV{AUTOMINER_CARDS_SPACES})
  {
    push @cmd, "--cuda-devices", "$ENV{AUTOMINER_CARDS_SPACES}";
  }

  print(" >> ethminer : @cmd\n");

  POSIX::dup2(1, 2) or die;
  exec { $cmd[0] } @cmd;
}
elsif($ARGV[0] eq "perf")
{
  while(my $line = <STDIN>)
  {
    chomp $line;

    # m  21:55:47|ethminer  Mining on PoWhash #90319290 : 30.71MH/s [A0+0:R0+0:F0]
    if($line =~ /Mining on PoWhash.*: ([0-9.]+)(k|m|g|t|p)?h\/s/i)
    {
      my $rate = $1;
      my $units = $2 || '';

      my $record = sprintf("%10u %s %sh/s\n", time() - $^T, $rate, $units);
      POSIX::write(1, $record, length($record));
    }
  }
}
