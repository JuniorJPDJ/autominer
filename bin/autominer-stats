#!/usr/bin/env perl

# Copyright (c) 2017 Todd Freed <todd.freed@gmail.com>
# 
# This file is part of autominer.
# 
# autominer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# autominer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License

use strict;
use warnings;

use File::Basename;
use Cwd;
our $repodir;
BEGIN { $repodir = dirname(Cwd::realpath("$0/..")) }
use lib "$repodir/lib";
use util;
use release;
override_warn_and_die();

use Data::Dumper;
use File::Find;
use Time::Piece;
use config;

our $verbose = 0;
our %opts = (
    'datadir'       => $ENV{HOME} . "/.autominer/data"
  , 'sort'          => 'h3'
  , 'profile'       => 'default'
);
my %optargs = (
    'help|h'        => \$opts{help}
  , 'version|V'     => \$opts{version}
  , 'verbose|v'     => \$opts{verbose}

  , 'data-dir=s'    => \$opts{"datadir"}
  , 'profile=s'     => \$opts{profile}
  , 'transitions'   => \$opts{transitions}
);
configure(\%optargs, \%opts);

if($opts{help})
{
  print <<HELP;
autominer - an autoswitching miner for linux, version $release::number

 >> BTC donations : 184AtMD3AihYke2bKQD9AXh4Dgzvtr7jhA (hossbeast)

usage: autominer stats [options]

 --help               print this help text
 --version            print the version number, exit
 --verbose            print commands as they are run

required
 --data-dir <path>    location for benchark and market data

optional
 --profile <name>     separate config file and benchmark data
 --transitions        show only transitions
HELP
  exit 0
}
if($opts{version})
{
  print "autominer-$release::number\n";
  exit 0;
}

my $mining_miner = '';
my $mining_algo = '';
my $unmatched = 0;

my $history_dir = "$opts{datadir}/history/$opts{profile}";
my $wanted = sub {
  return if $_ eq "." or $_ eq "..";

  # determine whether there was a transition
  readlink($_) =~ /\/([^\/]+)\/([^\/]+)\/[0-9]+$/;
my $fname = "$_";

  my $miner = $1;
  my $algo = $2;
  my $transition = 0;
  if($mining_miner ne $miner || $mining_algo ne $algo)
  {
    $transition = 1;
  }

  if($opts{transitions} and not $transition)
  {
    return;
  }

  my $fh;
  unless(open($fh, "<$history_dir/$_"))
  {
    $unmatched++;
    return;
  }

  my $header;
  my $footer;
  my $line;
  while(1)
  {
    my $s = <$fh>;
    last unless $s;
    chomp $s;
    $line = $s if $header;
    $header = $s unless $header;
  }
  $footer = $line;
  close($fh);

  if(not $header)
  {
    $unmatched++;
    return;
  }

  my $re = qr/
    ^
    (?:v1.0|v1.1|v1.2)
    \s+ ([0-9]+)                          # 1 unix timestamp
    \s+ ([a-zA-Z-_]+)                     # 2 market
    \s+ (?:[a-z0-9-_]+)\/(?:[a-z0-9-_]+)
    \s* \[
    \s* \(?\s*([0-9.]+)\s*\)?             # 3 opportunity rate
    \s* \(?\s*([0-9.]+)\s*\)?             # 4 average rate
    \s* \(?\s*([0-9.]+)\s*\)?             # 5 actual rate
    \s* \]\s+\*
    \s+ ([0-9.]+)\s+=                     # 6 average speed
    \s+ ([0-9.]+)                         # 7 predicted profit
    $
  /xi;

  if($header !~ $re)
  {
    $unmatched++;
    return;
  }

  my $start = $1;
  my $market = $2;
  my $opportunity_rate = $3;
  my $average_rate = $4;
  my $actual_rate = $5;
  my $average_speed = $6;
  my $predicted_profit = $7;
  my $mtime = (stat($_))[9];

  $re = qr/
    ^
    \s+ ([0-9.]+)\s+\*                    # 1 speed
    \s+ ([0-9.]+)\s+=                     # 2 rate
    \s+ ([0-9.]+)                         # 3 profit
    $
  /xi;

  my $actual_speed;
  my $actual_profit;
  my $profit_variance = 0;
  if($footer and $footer =~ $re)
  {
    $actual_speed = $1;
    $actual_rate = $2;
    $actual_profit = $3;

    my $profit_delta = $actual_profit - $predicted_profit;
    $profit_variance = $profit_delta / $predicted_profit;
    $profit_variance *= 100;
  }

  printf("%17s", localtime($start)->strftime("%D %T"));
  printf(" - %17s", localtime($mtime)->strftime("%D %T"));
  printf(" %30s", "$miner/$algo");
  printf(" %14.8f BTC/day", $actual_profit) if $actual_profit;
  printf(" variance %-4d%%", $profit_variance);
  printf(" predicted %14.8f", $opportunity_rate);
  printf(" * %14s", "(no data)") if $average_speed == 0xffffffff;
  printf(" * %14.8f", $average_speed) if $average_speed != 0xffffffff;
  printf(" = %14s", "(unknown)") if $average_speed == 0xffffffff;
  printf(" = %14.8f", $predicted_profit) if $average_speed != 0xffffffff;
  print(" <--") if $transition;
  print("\n");

  $mining_miner = $miner;
  $mining_algo = $algo;
};
my $preprocess = sub {
  sort { int($a) <=> int($b) } # increasing order
  grep { /^[0-9]+$/ } @_
};
chdir($history_dir) or die "chdir($history_dir) : $!";
finddepth({ wanted => $wanted, preprocess => $preprocess }, ".");

if ($unmatched and $verbose)
{
  print("ignored $unmatched records\n")
}
